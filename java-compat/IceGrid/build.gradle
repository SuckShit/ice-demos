//
// Copyright (c) ZeroC, Inc. All rights reserved.
//

def demoIceGridProject(name) {
    project(name) {
        slice {
            java {
                include = ["${projectDir}"]
                srcDir = '.'
            }
        }

        sourceSets {
            main {
                java {
                    srcDir '.'
                }
            }
        }

        dependencies {
            implementation localDependency("ice-compat")
            implementation localDependency("icegrid-compat")
            implementation localDependency("icelocatordiscovery-compat")
            runtimeOnly "org.apache.commons:commons-compress:1.14"
        }

        task makeDbDirs {
            doLast {
                file('db/registry').mkdirs()
                file('db/node').mkdirs()
            }
        }
        compileJava.dependsOn(makeDbDirs)

        clean {
            delete 'db'
        }

        // Don't create the default jar.
        jar.enabled = false
    }
}

demoIceGridProject(":demoIceGridSimple")
demoIceGridProject(":demoIceGridIcebox")

project(":demoIceGridSimple") {
    demoJar("client")
    demoJar("server")
}

project(":demoIceGridIcebox") {
    dependencies {
        implementation localDependency("icebox-compat")
    }
    demoJar("client")
    demoJar("service")
    demoIceBoxServer()
}
